RESTART VERSION=LAST,KEEP $ UM530 DBS=UM530
ID MSC,UM531 $ EXAMPLE
$REENTER 2 $ FROM RF TO DMAP MUST CONTROL REENTRY POINT
$READ 10 $ UM530
TIME 5
$COMPILER LIST,REF,DECK $ PRINT,CROSS REF, AND PUNCH (SEE COMPILE CARD BELOW)
DIAG 8 $ PRINT MATRIX TRAILERS AND RECOVERED DATA BLOCKS
$ DIAG 2 $ FILE ALLOCATION TABLE (FIAT)$ DIAG 2,3 DIFFERENT IN V66
$ DIAG 3 $ PRINT POOL CONTENTS (DPL)   $
DIAG 31 $ PRINT MODULE PROPERTIES LIST (MPL)
SOL 100 
MALTER 'MALTER:USERDMAP'
$BEGIN $ EXAMPLE TO PRINT DATA
TYPE PARM,NDDL,I,N,PEID,MPC,SPC,LOAD,LUSETS,SEID $
$ TYPE PARM,NDDL,CHAR8,N,HIGHQUAL
TYPE DB,CSTM,PG,KLL,ECTS,GPECT,SILS,GPLS $
TYPE DB,EST,KGG,BGPDTS,EQEXINS,GPDTS,USET $
TYPE DB,ETT,KFS $
PEID=0 $
SEID=0 $
MPC=0  $
SPC=102  $
LOAD=1 $
TABPT CSTM// $ CSTM IS A TABLE
MATPRN PG,KLL// $ PG IS A VECTOR KLL A MATRIX
PRTPARM // $ DISPLAY ALL PARAMETERS
MESSAGE //'LUSETS = '/LUSETS $
$ PRTPARM ///'LUSET' $ DISPLAY ONLY LUSET
ELTPRT ECTS,,,,,//1/S,N,ERROR
$MATPCH KLL//-6/'AA' $ PUNCH KLL ONTO DMI CARDS AND ECHO CARDS
$ REPLACE UNIT 6 IN MATPCH WITH VARIABLE FOR OUTPUT UNIT - PPR N66A-21
TYPE PARM,,I,N,OUT $
OUT= -GETSYS(OUT,2) $ GET OUTPUT UNIT FROM SYSTEM CELL 2, STORE IN OUT
MATPCH KLL//OUT/'AA' $ PUNCH KLL ONTO DMI CARDS AND ECHO CARDS
MATPRT KGG///1 $ MATPRT OF KGG
MATPRT PG//0/V,Y,NOPRT=-1 $ OPTIONALLY PRINT PG BY COLUMNS
TABPRT BGPDTS//'BGPDT' $
TABPRT CSTM // $
TABPRT EQEXINS//'EQEXIN' $
TABPRT GPDTS//'GPDT' $
TABPRT GPLS //'GPL' $
TABPRT USET,EQEXINS// $
TABPRT ETT //'ETT'  $
MATGPR GPLS,USET,SILS,KFS//'S'/'F'/ $ PRINT KFS WITH EXTERNAL ID
MATGPR GPLS,USET,SILS,KGG//'G'/'G'/'NULL' $ DISPLAY NULL COLUMNS
ENDALTER
LINK USERDMAP,INCL=MSCOBJ $
CEND


TITLE = THIS ILLUSTRATES THE OUTPUT TYPES                      UM531
LABEL = DMAP DOES NOT USE MUCH FROM CASE CONTROL DECK


BEGIN BULK
PARAM,NOPRT,1 $ PRINT PG THIS TIME
PARAM,UNUSED,1 $ UNUSED PARAMETER
ENDDATA
