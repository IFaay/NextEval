NASTRAN BUFFPOOL=30 $
INIT MASTER(S) $  SCR DATABASE AFTER EXECUTION
ID MSC,UM532 $ EXAMPLE
DIAG 8 $
TIME 5
$REENTER 2 $ FROM RF TO DMAP MUST CONTROL REENTRY POINT
$READ 10 $ UM530
$COMPILER LIST $ LIST DMAP
DIAG 8 $ PRINT MATRIX TRAILERS AND RECOVERED DATA BLOCKS
DIAG 56 $ PRINT DMAP STATMENTS AND RESTART DELETIONS DBS=UM530
SOL 100 
MALTER 'MALTER:USERDMAP'
TYPE DB,KLL,PL  $ LETS COMPILER KNOW THAT THESE DATABLOCKS RESIDE IN NDDL
$ TYPE ALL THE QUALIFIERS USED IN KLL AND PL
TYPE PARM,,I,Y,VERSID=1 $
$BEGIN $ DMAP TO ILLUSTRATE TWO WAYS TO SOLVE EQUATIONS
$
DBVIEW KLLR=KLL (WHERE WILDCARD=TRUE AND VERSION=VERSID) $
DBVIEW PLR=PL (WHERE WILDCARD=TRUE AND VERSION=VERSID) $
$
DIAGON(2) $
SOLVE KLLR,PLR,,,/U1 $ SOLVE EQUATION KLLR U1 = PLR FOR U1
DIAGOFF(2) $
MPYAD KLLR,U1,PLR/R///-1 $ COMPUTE KLLR U1 - PLR = R (RESIDUAL)
SOLVE KLLR,R,,,/DU $ SOLVE EQUATION KLLR R = DU FOR DU
ADD U1,DU/U2 $ ADD U1+DU = U2 (IMPROVED DISPLACEMENT)
MATPRN U2// $ PRINT ITERATED VECTOR
$
$ THE ABOVE DMAP DECOMPOSED KLLR TWICE
$ THIS CAN BE MADE MORE EFFICIENT AS FOLLOWS
$
DECOMP KLLR/LLL,ULL,///S,N,MINDIAG/S,N,DET/S,N,POW/S,N,SING $
COND SINGULAR,SING $ TEST FOR SINGULAR MATRIX
FBS LLL,ULL,PLR/UL $ SOLVE FOR UL(SAME AS U1 IN ABOVE)
MPYAD KLLR,UL,PLR/R1///-1 $ R1 = R ABOVE
FBS LLL,ULL,R/DU1 $ REUSE TRIANGULAR FACTORS TO GET DU1=DU ABOVE
ADD UL,DU1/U22/ $ U22=U2 ABOVE
MATPRN U22// $
EXIT $ STOP NORMAL RUN
LABEL SINGULAR $ EXCEPTION CONDITION FOR SINGULAR MATRIX
PRTPARM // $ DUMP PARAMETERS TO DETERMINE CAUSE OF SINGULARITY
ENDALTER
LINK USERDMAP,INCL=MSCOBJ $
CEND


TITLE = THIS DMAP ILLUSTRATES EQUATION SOLUTION                UM532
LABEL = DMAP DOES NOT USE MUCH FROM CASE CONTROL DECK


BEGIN BULK
ENDDATA
