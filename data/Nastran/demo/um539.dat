INIT MASTER(S)
INIT SCRATCH(MEM=100) $
ID MSC, UM539 $
$ Modified 4-Feb-2002 NingJun Teng for v2002
$ID UM539 MATRIXTOPOWER
DIAG 8 $ MATRIX TRAILERS
$BEGIN $ DMAP TO RAISE A MATRIX TO R-TH POWER
TIME 5
SOL 100 
MALTER 'MALTER:USERDMAP'
DMIIN DMI,DMINDX/Q,,,,,,,,,/ $
$
$ THIS DMAP WILL RAISE THE MATRIX Q TO THE R-TH POWER
$   WHERE R IS SPECIFIED BY THE USER
$
MATPRN Q// $ PRINT INCOMING MATRIX
PARAM //'SUB'/S,N,RR/V,Y,R=-1/2 $ COMPUTES RR=R-2 (NOTE R DEFAULTS
$   TO -1 WHICH IS AN ILLEGAL VALUE)
COND ERROR1,RR $ IF R IS ONE(1) OR LESS THIS PROCEDURE QUITS
ADD Q,/QQ/ $ THIS COPIES Q TO QQ
LABEL DOIT $ THIS IS THE TOP OF THE LOOP
      EQUIVX QQ/P/NEVER $ THIS BREAK EQUIV AT TOP OF LOOP
      MPYAD Q,QQ,/P/ $ P IS NOW Q*Q
      EQUIVX P/QQ/ALWAYS $ P IS NOW RENAMED TO QQ
      PARAM //'SUB'/S,N,RR/RR/1 $ DECREMENT RR BY ONE
      COND STOP,RR $ GO TO STOP IF Q HAS BEEN RAISED TO POWER R
REPT DOIT,10000 $ GO BACK TO DOIT A MAXIMUN OF 10000 TIMES
JUMP ERROR2 $ THIS IS AN IMPOSSIBLE PATH
$
$ THESE ARE BRANCH POINTS
$
LABEL STOP $ END OF JOB
MATPRN P// $ THE ANSWER
EXIT $ EQUIVALENT TO END
LABEL ERROR1 $ THE USER FORGOT TO SPECIFY R
PRTPARM // $ PRINT THE PARAMETERS
EXIT $
LABEL ERROR2 $ THE PROGRAM LOOPED 10000 TIMES
PRTPARM // $
$END $ LAST CARD OF DMAP
ENDALTER
LINK USERDMAP
CEND


TITLE = DMAP TO COMPUTE R-TH POWER OF A MATRIX Q               UM539
SUBTITLE = ILLUSTRATES LOOPING AND EQUIV
LABEL = USER SPECIFIES R


BEGIN BULK
$ INPUT Q
DMI,Q,0,6,1,0,,4,4
DMI,Q,1,1,2.,-1. $
DMI,Q,2,1,-1.,2.,-1. $
DMI,Q,3,2,-1.,2.,-1. $
DMI,Q,4,3,-1.,2. $
PARAM,R,4 $ THIS RUN ASKS FOR FOURTH POWER
ENDDATA
