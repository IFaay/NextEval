ID MSC, PROB7    $
$ Modified 2-Oct-2002 by P. R. Pamidi for v2002
$ Modified 4-Feb-2002 by NingJun Teng for v2002
$ID PROB7,DAY2
TIME 5
DIAG 8
SOL 100 
MALTER 'MALTER:USERDMAP'
$BEGIN $ DMAP EXAMPLE
$ INPUT SYMMETRIC BY PUTTING IN LOWER ONLY AND DIAG
DMIIN DMI,DMINDX/AL,D,BR,BI,,,,,,/ $
DTIIN DTI,DTINDX/FACTOR,,,,,,,,,/ $
TRNSP AL/AU $
ADD AL,AU/AMD $
ADD D,AMD/A $
MATPRN A// $
$
$ FORM A COMPLEX VECTOR
$
ADD BR,BI/BC/(1.,0.)/(0.,1.) $
DIAGONAL A/IDEN/'SQUARE'/0. $ IDENTY
SMPYAD BC,IDEN,BC,,,/BCTRBC/3////1 $
$
$ FIND EIGENVALUES OF A
$
READ A,IDEN,,,DYNAMICS,,CASECC,,,,,,,/LAMA,VECTOR,MI,OEIGS,ROOTS,/
   'MODES'/S,N,NEIGS/1 $QUOTES ADDED 3.2.87 TO AVOID UFM 3032
OFP LAMA,OEIGS // $
MATPRN VECTOR// $
$
$ USE PARAML TO EXTRACT FACTOR FOR DTI TABLE FACTOR
$
PARAML  FACTOR//'DTI'/1/1/S,N,FACTOR $
$
$ CONVERT TO COMPLEX
$
PARAMR //'COMPLEX'//FACTOR/0./S,N,FAC
$
$ USE AS COEF OF BC
$
ADD BC,/BCTF/FAC $
PRTPARM //$
MATPRN BR,BI,BC,BCTRBC// $ $
$
$ PUT THE RESULT ON DMI CARDS
$
$MATPCH BCTF//-6/FA $ CHANGED FROM UNIT 1 18 MAR 81
$ REPLACE UNIT 6 IN MATPCH WITH VARIABLE FOR OUTPUT UNIT - PPR N66A-21
TYPE PARM,,I,N,OUT $
OUT= -GETSYS(OUT,2) $ GET OUTPUT UNIT FROM SYSTEM CELL 2, STORE IN OUT
MATPCH BCTF//OUT $
$
$ DECOMPOSE WITH ALL GOODIES
$
DECOMP A/L,U,/-1//S,N,MINDIAG/S,N,DET/S,N,POWER/S,N,SING/
   S,N,NBRCHG/S,N,MAXRAT $
PARAM //'SUB'/S,N,NOGOOD/0/NBRCHG $
PARAM //'OR'/S,N,BAD/NOGOOD/SING $
COND QUIT,BAD $
JUMP GOGO $
LABEL QUIT $
PRTPARM // $
EXIT $
LABEL GOGO $
PRTPARM // $
FBS L,U,BCTF/ANSWER//V,Y,SIGNB $
MATPRN ANSWER// $
CEND


TITLE = HOMEWORK FOR DAY 2                                     PROB7
LABEL = S. E. WALL
METHOD = 33
$ LAST MODIFIED 18-APR-1983


BEGIN BULK
DMI,AL,0,6,1,0,,3,3
DMI,AL,1,2,-2.,-2. $
DMI,AL,2,3,4. $
DMI,D,0,3,1,0,,3,1 $
DMI,D,1,1,7.,1.,1. $
DMI,BR,0,2,1,0,,3,1 $
DMI,BR,1,1,1.,2.,3. $
DMI,BI,0,2,1,0,,3,1 $
DMI,BI,1,1,4.,5.,6. $
EIGR,33,GIV,,,,3,,,+EIGR
+EIGR,MASS $
DTI,FACTOR,0,1,1,1,1,1,1,+FACT1
+FACT1,ENDREC $
DTI,FACTOR,1,30.,40.,50.,ENDREC
ENDDATA
